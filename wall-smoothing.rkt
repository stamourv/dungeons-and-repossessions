#lang racket

(require "cell.rkt" "grid.rkt")

(provide smooth-walls)

;; wall smoothing, for aesthetic reasons
(define (smooth-walls grid)
  (for* ([x (in-range (grid-height grid))]
         [y (in-range (grid-width  grid))])
    (smooth-single-wall grid (vector x y)))
  (set! wall-cache (make-hash)) ; reset caches
  (set! free-cache (make-hash))
  grid)
(define free-cache (make-hash))
(define wall-cache (make-hash))
(define (smooth-single-wall grid pos)
  (define (counts-as-free? pos) ; i.e., player could be there
    (cond [(hash-ref free-cache pos #f) => values]
          [else
           (define c   (grid-ref grid pos))
           (define res (or (is-a? c empty-cell%) (is-a? c door%)))
           (hash-set! free-cache pos res)
           res]))
  (define (wall-or-door? pos)
    (cond [(hash-ref wall-cache pos #f) => values]
          [else
           (define c   (grid-ref grid pos))
           (define res (or (is-a? c wall%) (is-a? c door%)))
           (hash-set! wall-cache pos res)
           res]))
  (when (is-a? (grid-ref grid pos) wall%)
    (define u   (wall-or-door? (up    pos)))
    (define d   (wall-or-door? (down  pos)))
    (define l   (wall-or-door? (left  pos)))
    (define r   (wall-or-door? (right pos)))
    (define fu  (delay (counts-as-free? (up    pos))))
    (define fd  (delay (counts-as-free? (down  pos))))
    (define fl  (delay (counts-as-free? (left  pos))))
    (define fr  (delay (counts-as-free? (right pos))))
    (define ful (delay (counts-as-free? (up    (left  pos)))))
    (define fur (delay (counts-as-free? (up    (right pos)))))
    (define fdl (delay (counts-as-free? (down  (left  pos)))))
    (define fdr (delay (counts-as-free? (down  (right pos)))))
    (define (2-of-3? a b c) (or (and a b) (and a c) (and b c)))
    (array-set!
     grid pos
     (new
      (match* (u d l r)
        [(#F #F #F #F) pillar%]
        [(#F #F #F #T) horizontal-wall%]
        [(#F #F #T #F) horizontal-wall%]
        [(#F #F #T #T) horizontal-wall%]
        [(#F #T #F #F) vertical-wall%]
        [(#F #T #F #T) north-west-wall%]
        [(#F #T #T #F) north-east-wall%]
        ;; only have tees if enough corners are "inside"
        [(#F #T #T #T) (cond [(2-of-3? (force fu) (force fdl) (force fdr))
                              north-tee-wall%]
                             [(force fu)  horizontal-wall%]
                             [(force fdl) north-east-wall%]
                             [(force fdr) north-west-wall%])]
        [(#T #F #F #F) vertical-wall%]
        [(#T #F #F #T) south-west-wall%]
        [(#T #F #T #F) south-east-wall%]
        [(#T #F #T #T) (cond [(2-of-3? (force fd) (force ful) (force fur))
                              south-tee-wall%]
                             [(force fd)  horizontal-wall%]
                             [(force ful) south-east-wall%]
                             [(force fur) south-west-wall%])]
        [(#T #T #F #F) vertical-wall%]
        [(#T #T #F #T) (cond [(2-of-3? (force fl) (force fur) (force fdr))
                              west-tee-wall%]
                             [(force fl)  vertical-wall%]
                             [(force fur) south-west-wall%]
                             [(force fdr) north-west-wall%])]
        [(#T #T #T #F) (cond [(2-of-3? (force fr) (force ful) (force fdl))
                              east-tee-wall%]
                             [(force fr)  vertical-wall%]
                             [(force ful) south-east-wall%]
                             [(force fdl) north-east-wall%])]
        [(#T #T #T #T) (cond ; similar to the tee cases
                        [(or (and (force ful) (force fdr))
                             (and (force fur) (force fdl)))
                         ;; if diagonals are free, need a four-corner wall
                         four-corner-wall%]
                        [(and (force ful) (force fur)) south-tee-wall%]
                        [(and (force fdl) (force fdr)) north-tee-wall%]
                        [(and (force ful) (force fdl)) east-tee-wall%]
                        [(and (force fur) (force fdr)) west-tee-wall%]
                        [(force ful)                   south-east-wall%]
                        [(force fur)                   south-west-wall%]
                        [(force fdl)                   north-east-wall%]
                        [(force fdr)                   north-west-wall%])])))))


(module+ test
  (require rackunit)

  (define (render-grid g) (string-join g "\n" #:after-last "\n"))

  (define (check-smoothing t expected)
    (define g (parse-grid t))
    (check-equal? (show-grid (smooth-walls g))
                  (render-grid expected)))

  ;; Most of those are cases that the previous iterations of the smoothing
  ;; algorithm got wrong. So those are already mostly smoothed.

  (check-smoothing
   '(
     "..............╔═════╗......................................."
     "...╔═════╗....║     ║......................................."
     "...║     ║....║     ║......................................."
     "...║     ╠════╣     ║......................................."
     "...║     |    |     ║................╔════╗.......╔═════╗..."
     "...║     ╠════╩═╦-╦═╝................║    ║.......║     ║..."
     "...╚╦-╦══╣......║ ║...╔═══════╗......║    ║.......║     ║..."
     "....║ ║..╠══════╣ ║...║       ║......║    ╠═══════╣     ║..."
     "....║ ║..║      ║ ║...║       ╠══════╣    |       |     ║..."
     "....║ ║..║      ╠-╩═══╣       |      |    ╠═══════╣     ║..."
     "..╔═╩-╩═╗╣      |     |       ╠══════╩════╝.......║     ║..."
     "..║     ║║      ╠═════╩═══════╝...................║     ║..."
     "..║     ╠╚══════╝.................................╚═════╝..."
     "..║     ║..................................................."
     "..║     ║..................................................."
     "..╚═════╝..................................................."
     "............................................................"
     "............................................................"
     )
   '(
     "..............╔═════╗......................................."
     "...╔═════╗....║     ║......................................."
     "...║     ║....║     ║......................................."
     "...║     ╠════╣     ║......................................."
     "...║     |    |     ║................╔════╗.......╔═════╗..."
     "...║     ╠════╩═╦-╦═╝................║    ║.......║     ║..."
     "...╚╦-╦══╝......║ ║...╔═══════╗......║    ║.......║     ║..."
     "....║ ║..╔══════╣ ║...║       ║......║    ╠═══════╣     ║..."
     "....║ ║..║      ║ ║...║       ╠══════╣    |       |     ║..."
     "....║ ║..║      ╠-╩═══╣       |      |    ╠═══════╣     ║..."
     "..╔═╩-╩═╗║      |     |       ╠══════╩════╝.......║     ║..."
     "..║     ║║      ╠═════╩═══════╝...................║     ║..."
     "..║     ║╚══════╝.................................╚═════╝..."
     "..║     ║..................................................."
     "..║     ║..................................................."
     "..╚═════╝..................................................."
     "............................................................"
     "............................................................"
     ))

  (check-smoothing
   '(
     "............................................................"
     "..........................XXXXXX............................"
     "..........................X    X............................"
     "XXXXXXX...................X    XXXXXXXXXXXXXXX.............."
     "X     X...................X    |      |      X.....XXXXXXXXX"
     "X     X.....XXXXXXX.......X    XXXXXXXX      X.....X       X"
     "X     XXXXXXX     X.......XXXXXX......X      X.....X       X"
     "X     |     |     XXXXXXXXX    X......X      XXXXXXX       X"
     "X     X-XXXXX     |       |    XXXXXXXX      |     |       X"
     "X     X X...X     XXXXXXXXX    |      |      XXXXXXX       X"
     "XXXXXXX X...X     X.......X    XXXXXXXX      X.....X       X"
     "......X X...XXXXXXX.......XXXXXX......XXXXXXXX.....XXXXXXXXX"
     "....XXX-XXX................................................."
     "....X     X................................................."
     "....X     X................................................."
     "....X     X................................................."
     "....X     X................................................."
     "....XXXXXXX................................................."
     )
   '(
     "............................................................"
     "..........................╔════╗............................"
     "..........................║    ║............................"
     "╔═════╗...................║    ╠══════╦══════╗.............."
     "║     ║...................║    |      |      ║.....╔═══════╗"
     "║     ║.....╔═════╗.......║    ╠══════╣      ║.....║       ║"
     "║     ╠═════╣     ║.......╠════╣......║      ║.....║       ║"
     "║     |     |     ╠═══════╣    ║......║      ╠═════╣       ║"
     "║     ╠-╦═══╣     |       |    ╠══════╣      |     |       ║"
     "║     ║ ║...║     ╠═══════╣    |      |      ╠═════╣       ║"
     "╚═════╣ ║...║     ║.......║    ╠══════╣      ║.....║       ║"
     "......║ ║...╚═════╝.......╚════╝......╚══════╝.....╚═══════╝"
     "....╔═╩-╩═╗................................................."
     "....║     ║................................................."
     "....║     ║................................................."
     "....║     ║................................................."
     "....║     ║................................................."
     "....╚═════╝................................................."
     ))

  (check-smoothing
   '(
     "............................╔══════╗........................"
     "............................║      ║........................"
     "............................║      ║.................╔═════╗"
     "................╔═════╦═════╣      ║.................║     ║"
     "................║     |     |      ║.......╔════╦════╣     ║"
     "....╔═════╗.....║     ╠═════╣      ║.......║    |    |     ║"
     "....║     ╠═════╣     ║.....║      ╠═══════╣    ╠══╦-╣     ║"
     "....║     |     |     ║.....║      |       |    ║..║ ║     ║"
     "....║     ╠═══╦-╣     ║.....╚══════╩═══════╣    ║..║ ╠═════╝"
     "....║     ║...║ ║     ║....................║    ║..║ ║......"
     "....║     ║...║ ║     ║....................╚════╩╦═╩-╩═╗...."
     "....║     ║...║ ╚═════╝..........................║     ║...."
     "....║     ╚╔══╩-╩══╗.............................║     ║...."
     "....╚═════╝╗       ║.............................║     ║...."
     "...........║       ║.............................║     ║...."
     "...........║       ║.............................║     ║...."
     "...........║       ║.............................╚═════╝...."
     "...........╚═══════╝........................................"
     )
   '(
     "............................╔══════╗........................"
     "............................║      ║........................"
     "............................║      ║.................╔═════╗"
     "................╔═════╦═════╣      ║.................║     ║"
     "................║     |     |      ║.......╔════╦════╣     ║"
     "....╔═════╗.....║     ╠═════╣      ║.......║    |    |     ║"
     "....║     ╠═════╣     ║.....║      ╠═══════╣    ╠══╦-╣     ║"
     "....║     |     |     ║.....║      |       |    ║..║ ║     ║"
     "....║     ╠═══╦-╣     ║.....╚══════╩═══════╣    ║..║ ╠═════╝"
     "....║     ║...║ ║     ║....................║    ║..║ ║......"
     "....║     ║...║ ║     ║....................╚════╝╔═╩-╩═╗...."
     "....║     ║...║ ╠═════╝..........................║     ║...."
     "....║     ║╔══╩-╩══╗.............................║     ║...."
     "....╚═════╝║       ║.............................║     ║...."
     "...........║       ║.............................║     ║...."
     "...........║       ║.............................║     ║...."
     "...........║       ║.............................╚═════╝...."
     "...........╚═══════╝........................................"
     ))

  (check-smoothing
   '(
     "............................................................"
     "╔════╗..................................╔════╗.............."
     "║    ║..................................║    ║.............."
     "║    ║.....................╔═══════╗....║    ║.............."
     "║    ╠═══════╦═════╗.......║       ║....║    ╠════╦═════╗..."
     "║    |       |     ║.......║       ║....║    |    |     ║..."
     "║    ╠═══════╣     ╠═══════╣       ╠════╣    ╠═╦-╦╣     ║..."
     "║    ║.......║     |       |       |    |    ║.║ ║║     ║..."
     "║    ║.......║     ╠═══════╣       ╠════╣    ║.║ ║║     ║..."
     "╚════╝.......╚═════╝.......║       ║....╚════╣.║ ║║     ║..."
     "...........................╚═══════╝.........╠═╩-╩╩╦════╝..."
     ".............................................║     ║........"
     ".............................................║     ║........"
     ".............................................║     ║........"
     ".............................................║     ║........"
     ".............................................║     ║........"
     ".............................................╚═════╝........"
     "............................................................"
     )
   '(
     "............................................................"
     "╔════╗..................................╔════╗.............."
     "║    ║..................................║    ║.............."
     "║    ║.....................╔═══════╗....║    ║.............."
     "║    ╠═══════╦═════╗.......║       ║....║    ╠════╦═════╗..."
     "║    |       |     ║.......║       ║....║    |    |     ║..."
     "║    ╠═══════╣     ╠═══════╣       ╠════╣    ╠═╦-╦╣     ║..."
     "║    ║.......║     |       |       |    |    ║.║ ║║     ║..."
     "║    ║.......║     ╠═══════╣       ╠════╣    ║.║ ║║     ║..."
     "╚════╝.......╚═════╝.......║       ║....╚════╝.║ ║║     ║..."
     "...........................╚═══════╝.........╔═╩-╩╩╦════╝..."
     ".............................................║     ║........"
     ".............................................║     ║........"
     ".............................................║     ║........"
     ".............................................║     ║........"
     ".............................................║     ║........"
     ".............................................╚═════╝........"
     "............................................................"
     ))

  (check-smoothing
   '(
     ".......╔════╗..............................................."
     ".......║    ║.................................╔══════╗......"
     ".......║    ║.................................║      ║......"
     ".......║    ╠════╦═════╗......╔═══════╦═══════╣      ║......"
     ".......║    |    |     ║......║       |       |      ║......"
     ".......║    ╠════╣     ║......║       ╠═══════╣      ║......"
     ".......║    ║....║     ╠══════╣       ║.......╚══════╝......"
     ".......╚══╦-╣....║     |      |       ║....................."
     "..........║ ║....║     ╠══════╣       ║....................."
     "..........║ ║....║     ║......║       ║....................."
     "..........║ ║....╚═════╬═════╦╩═══════╝....................."
     "..........║ ║..........║     ║.............................."
     ".......╔══╩-╩══╦═══════╣     ║.............................."
     ".......║       |       |     ║.............................."
     ".......║       ╠═══════╣     ║.............................."
     ".......║       ║.......║     ║.............................."
     ".......║       ║.......╚═════╝.............................."
     ".......╚═══════╝............................................"
     )
   '(
     ".......╔════╗..............................................."
     ".......║    ║.................................╔══════╗......"
     ".......║    ║.................................║      ║......"
     ".......║    ╠════╦═════╗......╔═══════╦═══════╣      ║......"
     ".......║    |    |     ║......║       |       |      ║......"
     ".......║    ╠════╣     ║......║       ╠═══════╣      ║......"
     ".......║    ║....║     ╠══════╣       ║.......╚══════╝......"
     ".......╚══╦-╣....║     |      |       ║....................."
     "..........║ ║....║     ╠══════╣       ║....................."
     "..........║ ║....║     ║......║       ║....................."
     "..........║ ║....╚═════╬═════╗╚═══════╝....................."
     "..........║ ║..........║     ║.............................."
     ".......╔══╩-╩══╦═══════╣     ║.............................."
     ".......║       |       |     ║.............................."
     ".......║       ╠═══════╣     ║.............................."
     ".......║       ║.......║     ║.............................."
     ".......║       ║.......╚═════╝.............................."
     ".......╚═══════╝............................................"
     ))

  (check-smoothing
   '(
     "............................................................"
     "..........XXXXXXX...............XXXXXXXXX..................."
     "..........X     X....XXXXXXX....X       X..................."
     "..........X     XXXXXX     X....X       X..................."
     "..........X     |    |     X....X       X..................."
     "..........X     XXXXXX     X....X       X..................."
     "..........XXXXXXX....X     X....XXXX-XXXX..................."
     ".......XXXXXX........XXX-XXXXXXXXXXX X......................"
     ".......X    X....XXXXXXX X..X      X X....XXXXXXX..........."
     ".......X    XXXXXX     X X..X      X X....X     X..........."
     ".......X    |    |     X X..X      X-XXXXXX     X..........."
     ".......X    XXXXXX     X-XXXX      |      |     X..........."
     ".......X    X....X     |    |      XXXXXXXX     X..........."
     ".......XXXXXX....X     XXXXXX      X......X     X..........."
     ".................X     X....XXXXXXXX......X     X..........."
     ".................XXXXXXX..................XXXXXXX..........."
     "............................................................"
     "............................................................"
     )
   '(
     "............................................................"
     "..........╔═════╗...............╔═══════╗..................."
     "..........║     ║....╔═════╗....║       ║..................."
     "..........║     ╠════╣     ║....║       ║..................."
     "..........║     |    |     ║....║       ║..................."
     "..........║     ╠════╣     ║....║       ║..................."
     "..........╚═════╝....║     ║....╚══╦-╦══╝..................."
     ".......╔════╗........╚═╦-╦═╝╔══════╣ ║......................"
     ".......║    ║....╔═════╣ ║..║      ║ ║....╔═════╗..........."
     ".......║    ╠════╣     ║ ║..║      ║ ║....║     ║..........."
     ".......║    |    |     ║ ║..║      ╠-╩════╣     ║..........."
     ".......║    ╠════╣     ╠-╩══╣      |      |     ║..........."
     ".......║    ║....║     |    |      ╠══════╣     ║..........."
     ".......╚════╝....║     ╠════╣      ║......║     ║..........."
     ".................║     ║....╚══════╝......║     ║..........."
     ".................╚═════╝..................╚═════╝..........."
     "............................................................"
     "............................................................"
     ))

  (check-smoothing
   '(
     "............................................................"
     ".............XXXXXXX........................................"
     ".............X     X........................................"
     ".............X     XXXXXXXXXXXXX.......XXXXXX.......XXXXXXX."
     ".............X     |    |      X.......X    X.......X     X."
     ".....XXXXXXXXX     XX-XXX      XXXXXXXXX    X.......X     X."
     ".....X      XX     XX X.X      |       |    XXXXXXXXX     X."
     ".....X      XXXXXXXXX X.X      XXXXXXXXX    |       |     X."
     ".....X      XXXXXXXXX X.XXXXXXXX.......X    XXXXXXXXX     X."
     ".....X      |       | X................X    X.......XXXXXXX."
     ".....X      XXXXXXXXX X................X    X..............."
     ".....X      X.....XXX-XXX..............XXXXXX..............."
     ".....X      X.....X     X..................................."
     ".....XXXXXXXX.....X     X..................................."
     "..................X     X..................................."
     "..................X     X..................................."
     "..................X     X..................................."
     "..................XXXXXXX..................................."
     )
   '(
     "............................................................"
     ".............╔═════╗........................................"
     ".............║     ║........................................"
     ".............║     ╠════╦══════╗.......╔════╗.......╔═════╗."
     ".............║     |    |      ║.......║    ║.......║     ║."
     ".....╔══════╗║     ╠╦-╦═╣      ╠═══════╣    ║.......║     ║."
     ".....║      ║║     ║║ ║.║      |       |    ╠═══════╣     ║."
     ".....║      ║╚═════╝║ ║.║      ╠═══════╣    |       |     ║."
     ".....║      ╠═══════╣ ║.╚══════╝.......║    ╠═══════╣     ║."
     ".....║      |       | ║................║    ║.......╚═════╝."
     ".....║      ╠═══════╣ ║................║    ║..............."
     ".....║      ║.....╔═╩-╩═╗..............╚════╝..............."
     ".....║      ║.....║     ║..................................."
     ".....╚══════╝.....║     ║..................................."
     "..................║     ║..................................."
     "..................║     ║..................................."
     "..................║     ║..................................."
     "..................╚═════╝..................................."
     ))

  (check-smoothing
   '(
     ".......................XXXXXXXX....................XXXXXXXXX"
     ".......................X      X.......XXXXXX.......X       X"
     ".......................X      XXXXXXXXX    X.......X       X"
     ".......................X      |       |    X.......X       X"
     "............XXXXXXXX...X      XXXXXXXXX    XXXXXXXXX       X"
     "............X      X...XXXXXXXXXXX....X    |       |       X"
     "............X      XXXXXXX       X....X    XXX-XXXXX       X"
     "............X      |     |       XXXXXX    X.X X...X       X"
     "............X      XXXXXXX       |    |    X.X X...XXX-XXXXX"
     "............XXXXXXXX.....X       XXXXXXXXXXX.X X.....X X...."
     ".........................X       X........XXXX-XXX...X X...."
     ".........................XXXXXXXXX........X      X...X X...."
     "..........................................X      XXXXX-XXX.."
     "..........................................X      XX      X.."
     "..........................................X      XX      X.."
     "..........................................X      XX      X.."
     "..........................................XXXXXXXXX      X.."
     "..................................................XXXXXXXX.."
     )
   '(
     ".......................╔══════╗....................╔═══════╗"
     ".......................║      ║.......╔════╗.......║       ║"
     ".......................║      ╠═══════╣    ║.......║       ║"
     ".......................║      |       |    ║.......║       ║"
     "............╔══════╗...║      ╠═══════╣    ╠═══════╣       ║"
     "............║      ║...╚═╦════╩══╗....║    |       |       ║"
     "............║      ╠═════╣       ║....║    ╠═╦-╦═══╣       ║"
     "............║      |     |       ╠════╣    ║.║ ║...║       ║"
     "............║      ╠═════╣       |    |    ║.║ ║...╚═╦-╦═══╝"
     "............╚══════╝.....║       ╠════╩════╝.║ ║.....║ ║...."
     ".........................║       ║........╔══╩-╩═╗...║ ║...."
     ".........................╚═══════╝........║      ║...║ ║...."
     "..........................................║      ║╔══╩-╩═╗.."
     "..........................................║      ║║      ║.."
     "..........................................║      ║║      ║.."
     "..........................................║      ║║      ║.."
     "..........................................╚══════╝║      ║.."
     "..................................................╚══════╝.."
     ))

  (check-smoothing
   '(
     "............................................................"
     "....╔══════╗................................................"
     "....║      ║................................................"
     "....║      ║....╔═════╗....................................."
     "....║      ║....║     ║....................................."
     "....║      ║....║     ║....................................."
     "....╚══╦-╦═╩════╣     ║....................................."
     ".......║ |      |     ║.............╔═════╗................."
     "╔════╗.║ ╠══════╣     ║.............║     ║................."
     "║    ║.║ ║╝═════╣     ║.............║     ║................."
     "║    ╠═╩-╩╣     ╠═════╝.╔══════╦════╣     ║................."
     "║    |    |     ║.......║      |    |     ║................."
     "║    ╠════╣     ╠═══════╣      ╠════╣     ║................."
     "╚════╝....║     |       |      ║....║     ║................."
     "..........║     ╠═══════╣      ║....╚═════╝................."
     "..........║     ║.......╚══════╝............................"
     "..........╚═════╝..........................................."
     "............................................................"
     )
   '(
     "............................................................"
     "....╔══════╗................................................"
     "....║      ║................................................"
     "....║      ║....╔═════╗....................................."
     "....║      ║....║     ║....................................."
     "....║      ║....║     ║....................................."
     "....╚══╦-╦═╩════╣     ║....................................."
     ".......║ |      |     ║.............╔═════╗................."
     "╔════╗.║ ╠══════╣     ║.............║     ║................."
     "║    ║.║ ║╔═════╣     ║.............║     ║................."
     "║    ╠═╩-╩╣     ╠═════╝.╔══════╦════╣     ║................."
     "║    |    |     ║.......║      |    |     ║................."
     "║    ╠════╣     ╠═══════╣      ╠════╣     ║................."
     "╚════╝....║     |       |      ║....║     ║................."
     "..........║     ╠═══════╣      ║....╚═════╝................."
     "..........║     ║.......╚══════╝............................"
     "..........╚═════╝..........................................."
     "............................................................"
     ))
  )

#lang racket

(require "cell.rkt" "grid.rkt" "utils.rkt"
         "dungeon.rkt") ;; TODO eventually merge in with that

(define dungeon-height 18) ; to be easy to display in 80x24, with other stuff
(define dungeon-width  60)

(define (empty-grid)
  (array->mutable-array
   (build-array (vector dungeon-height dungeon-width)
                (lambda _ (new void-cell%)))))


(struct bsp
  (height
   width
   start-pos
   children)) ; (listof bsp?)
;; Note: children have one row/column of overlap, to have abutting walls

(define (show-bsp bsp)
  (define h (bsp-height bsp))
  (define w (bsp-width bsp))
  (define grid
    (array->mutable-array
     (for*/array #:shape (vector h w)
                 ([x (in-range h)]
                  [y (in-range w)])
        #f)))
  (define id 0)
  (define ids "0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ")
  (let loop ([bsp bsp])
    (define children (bsp-children bsp))
    (cond [(not (empty? children))
           (for-each loop children)]
          [else ; leaf, draw
           (for* ([dx (in-range (bsp-height bsp))]
                  [dy (in-range (bsp-width  bsp))])
             (define pos (down (right (bsp-start-pos bsp) dy) dx))
             (if (array-ref grid pos) ; already something there, so overlap zone
                 (array-set! grid pos " ")
                 (array-set! grid pos
                             (string-ref ids (modulo id (string-length ids))))))
           (set! id (add1 id))]))
  ;; actual display
  (with-output-to-string
    (lambda ()
      (for ([row (in-array-axis grid)])
        (for ([cell (in-array row)])
          (display (or cell "*")))
        (newline)))))

(module+ test
  (require rackunit)
  (define (render-grid g) (string-join g "\n" #:after-last "\n"))

  (check-equal? (show-bsp (bsp 5 5 #(0 0) #f))
                (render-grid '("00000"
                               "00000"
                               "00000"
                               "00000"
                               "00000")))
  (check-equal? (show-bsp (bsp 5 5 #(0 0)
                               (list (bsp 5 3 #(0 0) #f)
                                     (bsp 5 3 #(0 2) #f))))
                (render-grid '("00 11"
                               "00 11"
                               "00 11"
                               "00 11"
                               "00 11")))
  (check-equal? (show-bsp (bsp 5 5 #(0 0)
                               (list (bsp 5 3 #(0 0)
                                          (list (bsp 2 3 #(0 0) #f)
                                                (bsp 4 3 #(1 0) #f)))
                                     (bsp 5 3 #(0 2) #f))))
                (render-grid '("00 22"
                               "   22"
                               "11 22"
                               "11 22"
                               "11 22")))
  (check-equal? (show-bsp (bsp 5 5 #(0 0)
                               (list (bsp 5 3 #(0 0)
                                          (list (bsp 2 3 #(0 0) #f)
                                                (bsp 4 3 #(1 0) #f)))
                                     (bsp 5 3 #(0 2)
                                          (list (bsp 3 3 #(0 2) #f)
                                                (bsp 3 3 #(2 2) #f))))))
                (render-grid '("00 22"
                               "   22"
                               "11   "
                               "11 33"
                               "11 33")))
  )


(define min-room-height 6) ; maps are more height-constrained
(define min-room-width  9)

;; "Partitions" a grid randomly, by repeatedly splitting in two along an axis.
;; Not exactly a partition becase the two parts generated by a split overlap
;; by one row/column. That's to allow adjacent rooms to share walls, to save
;; space, look nicer, and give easy opportunities for connecting rooms.
(define (make-bsp [height    dungeon-height]
                  [width     dungeon-width]
                  [start-pos #(0 0)])
  ;; This threshold for splitting may result in areas that are too small
  ;; for rooms. That's ok, we can filter those out later. We'll end up with
  ;; more areas than we want rooms anyway.
  ;; We may end up with fewer rooms than we need for encounters, in which
  ;; case we can just restart. C'est la vie.
  (define split-slop              3) ; arbitrary
  (define can-split-horizontally? (>= width  (+ min-room-width  split-slop)))
  (define can-split-vertically?   (>= height (+ min-room-height split-slop)))
  ;; prioritize splitting the largest dimension. gives nicer results, IME
  (define split-horizontally? (and can-split-horizontally?
                                   (or (not can-split-vertically?)
                                       (>= width height))))
  (match-define (vector start-x start-y) start-pos)
  (bsp height
       width
       start-pos
       (cond
        [split-horizontally?
         (define split-dy (random-between 3 width)) ; 3 is arbitrary
         (list (make-bsp height (add1 split-dy) start-pos)
               (make-bsp height (- width split-dy) (right start-pos split-dy)))]
        [can-split-vertically?
         (define split-dx (random-between 3 height))
         (list (make-bsp (add1 split-dx) width start-pos)
               (make-bsp (- height split-dx) width (down start-pos split-dx)))]
        [else ; leaf, no children
         '()])))

(define (too-small? bsp)
  (or (< (bsp-height bsp) min-room-height)
      (< (bsp-width  bsp) min-room-width)))
(define (prune-bsp a-bsp)
  (match-define (bsp height width start-pos children) a-bsp)
  (bsp height width start-pos
       (map prune-bsp (filter (negate too-small?) children))))

(define (bsp-n-areas bsp)
  (define children (bsp-children bsp))
  (if (empty? children)
      1 ; leaf, just the one
      (for/sum ([c (in-list children)]) (bsp-n-areas c))))



(struct room
  (pos
   height
   width
   free-cells  ; where monsters or treasure could go
   extension-points)) ; (listof (cons/c pos direction))


(define (generate-rooms [a-bsp (prune-bsp (make-bsp))])
  (define grid
    (array->mutable-array
     (build-array (vector dungeon-height dungeon-width)
                  (lambda _ (new void-cell%)))))
  ;; put a room in each leaf area
  (define rooms
    (let loop ([a-bsp a-bsp])
      (match-define (bsp height width start-pos children) a-bsp)
      (cond
       [(not (empty? children)) ; recur
        (append-map loop children)]
       [else ; leaf, add a room
        (define-values (free-cells extension-points)
          (for*/fold ([free-cells       '()]
                      [extension-points '()])
              ([x (in-range height)]
               [y (in-range width)])
            (define pos (right (down start-pos x) y))
            (define (add-wall!) (array-set! grid pos (new wall%)))
            (cond [(or (and (= x 0) (= y 0)) ; corner wall
                       (and (= x (sub1 height)) (= y (sub1 width))))
                   (add-wall!)
                   ;; not free, and can't expand from corners
                   (values free-cells extension-points)]
                  [(= x 0) ; top wall
                   (add-wall!)
                   (values free-cells (dict-set extension-points pos up))]
                  [(= x (sub1 height)) ; bottom wall
                   (add-wall!)
                   (values free-cells (dict-set extension-points pos down))]
                  [(= y 0) ; left wall
                   (add-wall!)
                   (values free-cells (dict-set extension-points pos left))]
                  [(= y (sub1 width)) ; right wall
                   (add-wall!)
                   (values free-cells (dict-set extension-points pos right))]
                  [else ; inside of room
                   (array-set! grid pos (new empty-cell%))
                   (values (cons pos free-cells) extension-points)])))
        (list (room start-pos height width free-cells extension-points))])))
  (values grid rooms))


;; union-find data structure (dumb linear version)
(struct component (elt parent) #:mutable)
(define (singleton-component x)
  (define res (component x #f))
  (set-component-parent! res res)
  res)
(define (component-root c)
  (if (equal? (component-parent c) c)
      c
      (component-root (component-parent c))))
(define (component-union! x y)
  (set-component-parent! (component-root x) (component-root y)))
(define (same-component? x y)
  (equal? (component-root x) (component-root y)))

(define (add-corridors grid rooms)
  (define connected-pairs '()) ; (listof (list/c room? room?))
  (define rooms->connected-components
    (for/list ([r (in-list rooms)]
               [i (in-naturals)])
      (cons r (singleton-component i))))
  (define (connect-rooms! r1 r2)
    (set! connected-pairs (cons (cons r1 r2) connected-pairs))
    (component-union! (dict-ref rooms->connected-components r1)
                      (dict-ref rooms->connected-components r2)))
  (define (all-connected?)
    (= 1 (length (remove-duplicates
                  (map component-root (map cdr rooms->connected-components))))))

  ;; first, try opening doors between abutting rooms
  (for ([p (in-list (cartesian-product rooms rooms))])
    (match-define (list r1 r2) p)
    (unless (or (equal? r1 r2)
                ;; have we seen that pair before (in the other order)
                (member (cons r2 r1) connected-pairs))
      (define common ; get the positions first (extension points have dir too)
        (set-intersect (map car (room-extension-points r1))
                       (map car (room-extension-points r2))))
      (define possible-doors
        (filter values
                (for/list ([pos (in-list common)])
                  (cond [(and (counts-as-free? grid (up   pos))
                              (counts-as-free? grid (down pos)))
                         (cons pos horizontal-door%)]
                        [(and (counts-as-free? grid (left  pos))
                              (counts-as-free? grid (right pos)))
                         (cons pos vertical-door%)]
                        [else #f]))))
      (when (not (empty? possible-doors))
        (match-define (cons pos door-kind) (random-from possible-doors))
        (connect-rooms! r1 r2)
        (array-set! grid pos (new door-kind)))))

  ;; TODO dig corridors

  grid)


(module+ main
  ;; generate an example
  (define ex (make-bsp))
  (displayln (show-bsp ex))
  (displayln (show-bsp (prune-bsp ex)))
  (define-values (grid rooms) (generate-rooms (prune-bsp ex)))
  (displayln (show-grid (smooth-walls (add-corridors grid rooms))))

  ;; make sure that we have "enough" rooms with high-enough probability
  ;; (need enough for all encounters, and 6 is currently the max I've seen)
  (define n-tests 1000)
  (define histogram
    (for/fold ([hist '()])
        ([i n-tests])
      (define n (bsp-n-areas (prune-bsp (make-bsp))))
      (dict-update hist n add1 0)))
  (displayln "# room distribution")
  (for ([(k v) (in-dict (sort histogram < #:key car))])
    (printf "  ~a = ~a\n" (~a k #:width 2) v))
)
